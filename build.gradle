buildscript {	//빌드할 때 사용되는 스크립트.
	ext { 		//빌드 스크립트 내부에서의 변수
		springBootVersion = '2.3.1.RELEASE' //상수
	}
	repositories { //빌드할 때 의존성 저장소를 지정한다
		mavenCentral() //아래 dependencies 블록에 있는 애들이 전부 mavenCentral()에 등록된 애들!
	}
	dependencies { //빌드 시의 dependency
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE"
	}
}

subprojects { //요 내부에서 순서 중요!
	group 'com.peachbros'

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'

	sourceCompatibility = 1.8 //자바 1.8

	repositories {
		mavenCentral()
	}

	dependencies { //모든 하위 모듈에 공통으로 적용된다.

		implementation 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
		testImplementation 'io.rest-assured:rest-assured'
		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
		implementation 'org.springframework.boot:spring-boot-starter-security'
		testCompile("org.springframework.security:spring-security-test")
		runtimeOnly 'com.h2database:h2'
		compile('org.springframework.boot:spring-boot-starter-oauth2-client')
	}

	jacoco {
		toolVersion = '0.8.5'
	}

	jacocoTestReport {
		reports {
			html.enabled true
			xml.enabled false
			csv.enabled true
		}
		finalizedBy 'jacocoTestCoverageVerification'
	}

	test {
		useJUnitPlatform()
		finalizedBy 'jacocoTestReport'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}
}