plugins {
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'jacoco'
}

group = 'com.peachbros'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation("com.google.guava:guava:29.0-jre")

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

jacoco {
	toolVersion = "0.8.5"
}

test {
	useJUnitPlatform()

	finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
	reports {
		html.enabled true
		xml.enabled false
		csv.enabled false
	}

	finalizedBy 'jacocoTestCoverageVerification'
}


jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'

			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.70
			}

			includes = ['*.domain.*']
		}

		rule {
			element = 'METHOD'

			limit {
				counter = 'LINE'
				value = 'TOTALCOUNT'
				maximum = 22
			}

			includes = ['*.domain.*']
		}

	}
}
